plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'jacoco'
}

pitest {
    junit5PluginVersion = '0.15'
}

jacoco {
    toolVersion = "0.8.7"
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

group = 'com.optivem'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.optivem.kata.banking.bdd.glue', 'src/test/resources']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    //runtimeOnly 'org.postgresql:postgresql:42.3.6'
    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.0'
    implementation 'de.huxhorn.sulky:de.huxhorn.sulky.ulid:8.3.0'
    implementation 'net.sizovs:pipelinr:0.7'
    implementation 'de.mkammerer.snowflake-id:snowflake-id:0.0.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.11'
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    testImplementation 'com.athaydes:spock-reports:2.3.0-groovy-3.0', { transitive = false }
    testImplementation 'io.cucumber:cucumber-java:7.3.3'
    testImplementation 'io.cucumber:cucumber-junit:7.3.3'
    testImplementation 'io.cucumber:cucumber-spring:7.3.3'
    testImplementation 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}
